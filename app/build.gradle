buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "http://dl.bintray.com/kiall/exoplayer" }
    mavenCentral()
}

apply plugin: 'io.fabric'
apply from: '../signing.gradle'

android {
    compileSdkVersion 28
    defaultConfig {

        minSdkVersion 16
        targetSdkVersion 28

        versionCode 172
        versionName "2.2.13"

        // Define the apk naming convention
        applicationId "org.tvheadend.tvhclient"
        setProperty("archivesBaseName", "${applicationId}-${versionName}-${versionCode}")

        // Set the default values for these build config fields
        buildConfigField "String", "DEVELOPER_EMAIL", "\"\""
        buildConfigField "String", "PUBLIC_KEY_SALT", "\"\""
        buildConfigField "Integer", "BUILD_VERSION", "$versionCode"

        // Include the build time and git commit hash in the crash reports
        def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
        def buildTime = new GregorianCalendar().format("yyyy.MM.dd HH:mm:ss")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        // The cast ID that the app shall use
        buildConfigField "String", "CAST_ID", "\"0531DF56\""

        // Support vector drawable and animated vector drawable on devices
        // running platform versions lower than Android 5.0 (API level 21)
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        lintOptions {
            // Do not check for lint errors to reduce the  build time.
            // Lint checks will be done manually before a release
            checkReleaseBuilds false
        }
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    buildTypes {
        debug {
            // Show all features when true
            buildConfigField "Boolean", "UNLOCKED", "false"
            // Enable all logging, otherwise it is suppressed
            buildConfigField "Boolean", "DEBUG_LOG", "true"
        }
        release {
            // Show all features when true
            buildConfigField "Boolean", "UNLOCKED", "false"
            // Enable all logging, otherwise it is suppressed
            buildConfigField "Boolean", "DEBUG_LOG", "true"

            // The property file must exist in release mode so that the
            // actual values can be assigned from the property file to these fields.
            if (tvhHasProperty("developerEmail")) {
                buildConfigField "String", "DEVELOPER_EMAIL", "\"" + tvhProperty("developerEmail") + "\""
            }
            if (tvhHasProperty("publicKeySalt")) {
                buildConfigField "String", "PUBLIC_KEY_SALT", "\"" + tvhProperty("publicKeySalt") + "\""
            }

            // Reduce the apk file size by enabling proguard
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
    }
}

dependencies {
    // Dependency on local binaries
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support library basics
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha05'
    implementation 'androidx.annotation:annotation:1.1.0-rc01'
    implementation 'androidx.preference:preference:1.1.0-alpha05'
    implementation 'com.google.android.material:material:1.1.0-alpha06'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"
    implementation 'androidx.core:core-ktx:1.2.0-alpha01'

    // Casting
    implementation 'com.google.android.gms:play-services-cast-framework:16.2.0'
    implementation 'androidx.mediarouter:mediarouter:1.1.0-beta01'

    // Billing
    implementation 'com.android.billingclient:billing:1.2'

    // Material navigation drawer and dialogs
    def dialog_version = '3.0.0-beta2'
    implementation "com.afollestad.material-dialogs:core:$dialog_version"
    implementation "com.afollestad.material-dialogs:input:$dialog_version"
    implementation "com.afollestad.material-dialogs:files:$dialog_version"
    implementation "com.afollestad.material-dialogs:datetime:$dialog_version"
    implementation "com.afollestad.material-dialogs:lifecycle:$dialog_version"
    implementation 'com.mikepenz:materialdrawer:7.0.0-beta01'

    // Memory leaks and crash analytics
    def leakcanary_version = '1.6.3'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakcanary_version"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.0'

    // Life cycles, LiveData and ViewModel
    def lifecycle_version = "2.2.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Workmanager
    implementation 'android.arch.work:work-runtime:1.0.1'

    // Android Room
    def room_version = "2.1.0-beta01"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Dependency injection
    implementation 'com.google.dagger:dagger:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'

    // Logging library
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Image loading library
    implementation 'com.squareup.picasso:picasso:2.71828'

    // App data access via browser
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // Internal player
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.1k1'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.7.1k1'
    implementation 'com.google.android.exoplayer:extension-ffmpeg:2.7.1k1'

    // Use multidex because the method count is over 64k
    implementation 'androidx.multidex:multidex:2.0.1'

    // Required to connect to the tvheadend server
    implementation project(path: ':htsp')
}
