buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "http://dl.bintray.com/kiall/exoplayer" }
}

apply plugin: 'io.fabric'
apply from: '../signing.gradle'

android {
    compileSdkVersion 28
    defaultConfig {

        minSdkVersion 16
        targetSdkVersion 28

        versionCode 146
        versionName "2.0.4"

        // Define the apk naming convention
        applicationId "org.tvheadend.tvhclient"
        setProperty("archivesBaseName", "${applicationId}-${versionName}-${versionCode}")

        // Set the default values for these build config fields
        buildConfigField "String", "DEVELOPER_EMAIL", "\"\""
        buildConfigField "String", "PUBLIC_KEY_SALT", "\"\""
        buildConfigField "Integer", "BUILD_VERSION", "$versionCode"

        // Include the build time and git commit hash in the crash reports
        def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
        def buildTime = new GregorianCalendar().format("yyyy.MM.dd HH:mm:ss")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        // The cast ID that the app shall use
        buildConfigField "String", "CAST_ID", "\"0531DF56\""

        // Support vector drawable and animated vector drawable on devices
        // running platform versions lower than Android 5.0 (API level 21)
        vectorDrawables.useSupportLibrary = true

        lintOptions {
            // Do not check for lint errors to reduce the  build time.
            // Lint checks will be done manually before a release
            checkReleaseBuilds false
        }
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            multiDexEnabled true
            // Show all features when true
            buildConfigField "Boolean", "UNLOCKED", "false"
            // Enable all logging, otherwise it is suppressed
            buildConfigField "Boolean", "DEBUG_LOG", "true"
        }
        release {
            // Show all features when true
            buildConfigField "Boolean", "UNLOCKED", "false"
            // Enable all logging, otherwise it is suppressed
            buildConfigField "Boolean", "DEBUG_LOG", "true"

            // The property file must exist in release mode so that the
            // actual values can be assigned from the property file to these fields.
            if (tvhHasProperty("developerEmail")) {
                buildConfigField "String", "DEVELOPER_EMAIL", "\"" + tvhProperty("developerEmail") + "\""
            }
            if (tvhHasProperty("publicKeySalt")) {
                buildConfigField "String", "PUBLIC_KEY_SALT", "\"" + tvhProperty("publicKeySalt") + "\""
            }

            // Reduce the apk file size by enabling proguard
            //minifyEnabled true
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Dependency on local binaries
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support library basics
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-beta01'
    implementation 'androidx.annotation:annotation:1.0.1'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Casting
    implementation 'com.google.android.gms:play-services-cast-framework:16.1.2'
    implementation 'androidx.mediarouter:mediarouter:1.0.0-beta01'

    // Billing
    implementation 'com.android.billingclient:billing:1.2'

    // Material navigation drawer and dialogs
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.mikepenz:materialdrawer:6.0.9'

    // View binding
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    // Memory leaks and crash analytics
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

    // Architecture components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.0.0-beta01'
    implementation 'androidx.room:room-runtime:2.0.0-beta01'
    implementation 'android.arch.work:work-runtime:1.0.0-beta03'
    annotationProcessor 'androidx.room:room-compiler:2.0.0-beta01'

    // Dependency injection
    implementation 'com.google.dagger:dagger:2.21'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.21'

    // Logging library
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Image loading library
    implementation 'com.squareup.picasso:picasso:2.71828'

    // App data access via browser
    implementation 'com.facebook.stetho:stetho:1.5.0'

    // Internal player
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.1k1'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.7.1k1'
    implementation 'com.google.android.exoplayer:extension-ffmpeg:2.7.1k1'

    // Use multidex because the method count is over 64k
    debugImplementation 'androidx.multidex:multidex:2.0.1'
}
