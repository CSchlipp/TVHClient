apply plugin: 'com.android.application'

apply from: '../version.gradle'
apply from: '../signing.gradle'
apply from: '../acra.gradle'
apply from: '../git.gradle'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        versionCode project.ext.versionCode
        versionName project.ext.versionName

        applicationId "org.tvheadend.tvhclient"
        setProperty("archivesBaseName", "${applicationId}-${versionName}-${versionCode}")

        // Set the default values for these build config fields
        buildConfigField "String", "DEVELOPER_EMAIL", "\"\""
        buildConfigField "String", "PUBLIC_KEY_SALT", "\"\""
    }

    buildTypes {
        debug {
            // Show all features of the app when the app is not released and under development
            buildConfigField "Boolean", "DEBUG_MODE", "true"
        }
        release {
            // Add the values from the local properties file
            if (tvhHasProperty("developerEmail")) {
                buildConfigField "String", "DEVELOPER_EMAIL", "\"" + tvhProperty("developerEmail") + "\""
            }
            if (tvhHasProperty("publicKeySalt")) {
                buildConfigField "String", "PUBLIC_KEY_SALT", "\"" + tvhProperty("publicKeySalt") + "\""
            }
            // Disable the unlocked features when the app is released.
            // This must be done by the in-app purchase
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.google.android.gms:play-services-cast:11.0.4'
    compile 'com.android.support:mediarouter-v7:25.3.1'
    compile 'com.google.android.libraries.cast.companionlibrary:ccl:2.8.3'
    compile 'com.anjlab.android.iab.v3:library:1.0.32'
    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.afollestad.material-dialogs:core:0.9.0.1'
    compile 'ch.acra:acra:4.9.2'
}
