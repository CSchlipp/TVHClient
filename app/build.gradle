buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply from: '../version.gradle'
apply from: '../signing.gradle'
apply from: '../git.gradle'

android {
    compileSdkVersion 27
    defaultConfig {

        minSdkVersion 16
        targetSdkVersion 27

        versionCode project.ext.versionCode
        versionName project.ext.versionName

        applicationId "org.tvheadend.tvhclient"
        setProperty("archivesBaseName", "${applicationId}-${versionName}-${versionCode}")

        // Set the default values for these build config fields
        buildConfigField "String", "DEVELOPER_EMAIL", "\"\""
        buildConfigField "String", "PUBLIC_KEY_SALT", "\"\""
        buildConfigField "Integer", "BUILD_VERSION", "$versionCode"

        vectorDrawables.useSupportLibrary = true

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            // Show all features of the app when the app is not released and under development
            buildConfigField "Boolean", "DEBUG_MODE", "true"
        }
        release {
            // Add the values from the local properties file
            if (tvhHasProperty("developerEmail")) {
                buildConfigField "String", "DEVELOPER_EMAIL", "\"" + tvhProperty("developerEmail") + "\""
            }
            if (tvhHasProperty("publicKeySalt")) {
                buildConfigField "String", "PUBLIC_KEY_SALT", "\"" + tvhProperty("publicKeySalt") + "\""
            }
            // TODO Disable the unlocked features when the app is released.
            // This must be done by the in-app purchase
            buildConfigField "Boolean", "DEBUG_MODE", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue("string", "PORT_NUMBER", "8081")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.google.android.gms:play-services-cast-framework:15.0.1'
    implementation 'com.android.support:mediarouter-v7:27.1.1'
    implementation 'com.anjlab.android.iab.v3:library:1.0.44'
    implementation 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
    //compile 'com.nineoldandroids:library:2.4.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    //compile 'ch.acra:acra:4.9.2'
    implementation 'com.mikepenz:materialdrawer:6.0.8'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.3'
    // Architecture components
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:common-java8:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    implementation "android.arch.work:work-runtime:1.0.0-alpha04"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    // Injection library
    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    // Logging library
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'
    // Image loading library
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'com.facebook.stetho:stetho:1.5.0'
    //implementation project(':stetho')
}
