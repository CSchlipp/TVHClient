buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply from: '../version.gradle'
apply from: '../signing.gradle'
apply from: '../git.gradle'

android {
    compileSdkVersion 27
    defaultConfig {

        minSdkVersion 16
        targetSdkVersion 27
        multiDexEnabled true

        versionCode project.ext.versionCode
        versionName project.ext.versionName

        // Define the structure of the apk file name
        applicationId "org.tvheadend.tvhclient"
        setProperty("archivesBaseName", "${applicationId}-${versionName}-${versionCode}")

        // Set the default values for these build config fields
        buildConfigField "Boolean", "DEBUG_MODE", "false"
        buildConfigField "String", "DEVELOPER_EMAIL", "\"\""
        buildConfigField "String", "PUBLIC_KEY_SALT", "\"\""
        buildConfigField "Integer", "BUILD_VERSION", "$versionCode"

        // Get the build time and git commit hash so it can be included in the crash reports
        def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
        def buildTime = new GregorianCalendar().format("yyyy.MM.dd HH:mm:ss")
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        // The cast ID that the app shall use
        buildConfigField "String", "CAST_ID", "\"0531DF56\""

        vectorDrawables.useSupportLibrary = true

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        lintOptions {
            checkReleaseBuilds false
            // Or, if you prefer, you can continue to check for errors in release builds,
            // but continue the build even when errors are found:
            //abortOnError false
        }
    }
    buildTypes {
        debug {
            // Show all features of the app when the app is not released and under development
            buildConfigField "Boolean", "DEBUG_MODE", "true"
        }
        release {
            // The property file must exist in release mode so that the
            // actual values can be assigned from the property file to these fields.
            if (tvhHasProperty("developerEmail")) {
                buildConfigField "String", "DEVELOPER_EMAIL", "\"" + tvhProperty("developerEmail") + "\""
            }
            if (tvhHasProperty("publicKeySalt")) {
                buildConfigField "String", "PUBLIC_KEY_SALT", "\"" + tvhProperty("publicKeySalt") + "\""
            }

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // Casting
    implementation 'com.google.android.gms:play-services-cast-framework:16.0.2'
    implementation 'com.android.support:mediarouter-v7:27.1.1'
    // Misc libraries
    implementation 'com.anjlab.android.iab.v3:library:1.0.44'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.mikepenz:materialdrawer:6.0.8'
    // View binding
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // Memory leaks and crash analytics
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'
    // Architecture components
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:common-java8:1.1.1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    implementation "android.arch.work:work-runtime:1.0.0-alpha08"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    // Dependency injection
    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    // Logging library
    implementation 'com.jakewharton.timber:timber:4.5.1'
    // Image loading library
    implementation 'com.squareup.picasso:picasso:2.71828'
    // App data access via browser
    implementation 'com.facebook.stetho:stetho:1.5.0'
    // Internal player
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.1k1'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.7.1k1'
    implementation 'com.google.android.exoplayer:extension-ffmpeg:2.7.1k1'
    // Use multidex because the method count is over 64k
    implementation 'com.android.support:multidex:1.0.3'
}
